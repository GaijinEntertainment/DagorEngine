// If the FSR was used during gbuffer rendering, then this var will have YES for the whole frame
int fsr_distortion = 0;
interval fsr_distortion: no < 1, yes;

int fsr_resolve_lut_id = 0;
int fsr_distortion_lut_id = 0;

macro USE_FSR(code)
  if (hardware.ps5 && fsr_distortion == yes)
  {
    (code)
    {
      fsr_lut_id@i2 = (fsr_resolve_lut_id, fsr_distortion_lut_id);
      fsr_viewport@f4 = get_viewport();
    }

    hlsl(code)
    {
      RegularBuffer<sce::Gnm::Texture> fsr_static_textures : BINDLESS_TEX_REGISTER;
      RegularBuffer<sce::Gnm::Sampler> fsr_static_samplers : BINDLESS_SAMPLER_REGISTER;

      float2 linearToDistortedTc(float2 tc)
      {
        Texture1D<float> fsrLutH = fsr_static_textures[fsr_lut_id.x];
        Texture1D<float> fsrLutV = fsr_static_textures[fsr_lut_id.x + 1];
        SamplerState smp = fsr_static_samplers[0];

        return float2(fsrLutH.Sample(smp, tc.x), fsrLutV.Sample(smp, tc.y));
      }

      float2 distortedToLinearTc(float2 tc)
      {
        Texture1D<float> fsrLutH = fsr_static_textures[fsr_lut_id.y];
        Texture1D<float> fsrLutV = fsr_static_textures[fsr_lut_id.y + 1];
        SamplerState smp = fsr_static_samplers[0];

        return float2(fsrLutH.Sample(smp, tc.x), fsrLutV.Sample(smp, tc.y));
      }

      uint2 linearToDistortedPos(uint2 pos, uint2 size)
      {
        float2 tc = float2(pos) / float2(size);
        return uint2(linearToDistortedTc(tc) * size);
      }

      uint2 distortedToLinearPos(uint2 pos, uint2 size)
      {
        float2 tc = float2(pos) / float2(size);
        return uint2(distortedToLinearTc(tc) * size);
      }

      float2 getDistortedTc(float2 pos)
      {
        return (pos - fsr_viewport.xy) / fsr_viewport.zw;
      }

      float2 getDistortedTc(float2 pos, float2 tc)
      {
        return getDistortedTc(pos);
      }

      #define FSR_DISTORTION 1
    }
  }
  else
  {
    hlsl(code)
    {
      uint2 linearToDistortedPos(uint2 pos, uint2 size)
      {
        return pos;
      }

      uint2 distortedToLinearPos(uint2 pos, uint2 size)
      {
        return pos;
      }

      float2 linearToDistortedTc(float2 tc)
      {
        return tc;
      }

      float2 distortedToLinearTc(float2 tc)
      {
        return tc;
      }

      float2 getDistortedTc(float2 pos, float2 tc)
      {
        return tc;
      }
    }
  }
endmacro
