include "vr_reprojection.dshl"

int use_vr_multiview = 0;
interval use_vr_multiview : no < 1, yes;

macro VR_MULTIVIEW_RENDERING(enable, use_dynstcode)
  if (hardware.dx12 || (hardware.ps5 && use_vr_multiview == yes && (enable)))
  {
    if (use_dynstcode)
    {
      INIT_VR_REPROJECTION(vs)
    }

    if (use_vr_multiview == yes && (enable))
    {
      view_instances = 2;
    }

    if (hardware.dx12)
    {
      hlsl {
        #define USE_VIEW_ID , uint viewId : SV_ViewID
        #define VR_MULTIVIEW_REPROJECT(pos) { if (viewId == 1) (pos) = mul((pos), vrReprojectionMatrix); }
      }
    }
    else if (hardware.ps5)
    {
      hlsl {
        #pragma argument(stereo=x)

        #define USE_VIEW_ID , out float pos2 : S_STEREO_POSITION
        #define VR_MULTIVIEW_REPROJECT(pos) { pos2 = mul((pos), vrReprojectionMatrix).x; }
      }
    }

  }
  else
  {
    hlsl {
      #define USE_VIEW_ID
      #define VR_MULTIVIEW_REPROJECT(pos)
    }
  }
endmacro