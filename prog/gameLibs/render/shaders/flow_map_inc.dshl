int max_flowmap_cascades = 3;
interval max_flowmap_cascades: two<3, three;

macro INIT_WATER_FLOWMAP()
  texture water_flowmap_tex;
  texture water_flowmap_tex_add_0;
  texture water_flowmap_tex_add_1a;
  texture water_flowmap_tex_add_1b;
  texture water_flowmap_tex_add_2a;
  texture water_flowmap_tex_add_2b;
  texture water_flowmap_tex_blur_1a;
  texture water_flowmap_tex_blur_1b;
  texture water_flowmap_tex_blur_2a;
  texture water_flowmap_tex_blur_2b;
  float4 world_to_flowmap = (1/32,1/32,0.5,0.5);
  float4 world_to_flowmap_add_0 = (1,1,0,0);
  float4 world_to_flowmap_add_1a = (1,1,1,1);
  float4 world_to_flowmap_add_1b = (0,0,0,0);
  float4 world_to_flowmap_add_2a = (1,1,1,1);
  float4 world_to_flowmap_add_2b = (0,0,0,0);
  float water_wind_strength = 0.2;
  float water_flowmap_range_0 = 100;
  float water_flowmap_range_1 = 100;
  float water_flowmap_fading = 3;
  float4 water_flowmap_strength = (1,5,0.5,0); // speed, foam, curvature, mask
  float4 water_flowmap_strength_add = (0.5,1,1,0.3); // min distance,max distance,speed,foam
  float4 water_flowmap_foam = (5,10,0.5,0.1); // power, scale, threshold, reflectivity
  float water_flowmap_foam_tiling = 1;
  float water_flowmap_foam_displacement = 0.1;
  float water_flowmap_foam_softness = 1.0;
  float water_flowmap_fx_speed = 0.1;
  float water_flowmap_debug = 0;
  float water_flowmap_multiplier = 1;
  float water_flowmap_blend = 0;
  float4 water_fluid_strength = (0.2, 20, 1, 0.1);
  float4 water_fluid_brightness = (4, 2, 5, 0);
  float water_flowmap_bump_scale = 1;
  int water_flowmap_cascades = 0;
endmacro

macro USE_WATER_FLOWMAP(code)
  (code)
  {
    water_flowmap_tex@smp2d = water_flowmap_tex;
    water_flowmap_tex_add_0@tex = water_flowmap_tex_add_0 hlsl{ Texture2D<float4>water_flowmap_tex_add_0@tex; };
    water_flowmap_tex_add_1a@tex = water_flowmap_tex_add_1a hlsl{ Texture2D<float4>water_flowmap_tex_add_1a@tex; };
    water_flowmap_tex_add_1b@tex = water_flowmap_tex_add_1b hlsl{ Texture2D<float4>water_flowmap_tex_add_1b@tex; };
    if (max_flowmap_cascades == three)
    {
      water_flowmap_tex_add_2a@tex = water_flowmap_tex_add_2a hlsl{ Texture2D<float4>water_flowmap_tex_add_2a@tex; };
      water_flowmap_tex_add_2b@tex = water_flowmap_tex_add_2b hlsl{ Texture2D<float4>water_flowmap_tex_add_2b@tex; };
    }
    water_flowmap_tex_blur_1a@tex = water_flowmap_tex_blur_1a hlsl{ Texture2D<float4>water_flowmap_tex_blur_1a@tex; };
    water_flowmap_tex_blur_1b@tex = water_flowmap_tex_blur_1b hlsl{ Texture2D<float4>water_flowmap_tex_blur_1b@tex; };
    if (max_flowmap_cascades == three)
    {
      water_flowmap_tex_blur_2a@tex = water_flowmap_tex_blur_2a hlsl{ Texture2D<float4>water_flowmap_tex_blur_2a@tex; };
      water_flowmap_tex_blur_2b@tex = water_flowmap_tex_blur_2b hlsl{ Texture2D<float4>water_flowmap_tex_blur_2b@tex; };
    }
    world_to_flowmap@f4 = world_to_flowmap;
    world_to_flowmap_add_0@f4 = world_to_flowmap_add_0;
    world_to_flowmap_add_1a@f4 = world_to_flowmap_add_1a;
    world_to_flowmap_add_1b@f4 = world_to_flowmap_add_1b;
    world_to_flowmap_add_2a@f4 = world_to_flowmap_add_2a;
    world_to_flowmap_add_2b@f4 = world_to_flowmap_add_2b;
    cascadesTexelScale_current_time_water_wind_strength@f3 = (cascadesTexelScale0123.x, time_phase(0, 0), water_wind_strength, 0);
    water_flowmap_fading_inv_fading@f2 = (water_flowmap_fading, 1 / water_flowmap_fading, 0, 0);
    water_flowmap_strength@f4 = (water_flowmap_strength);
    water_flowmap_strength_add_0@f4 = (-1/((water_flowmap_strength_add.y-water_flowmap_strength_add.x)*water_flowmap_range_0),
      water_flowmap_strength_add.y/(water_flowmap_strength_add.y-water_flowmap_strength_add.x), water_flowmap_strength_add.z, water_flowmap_strength_add.w);
    water_flowmap_strength_add_1@f4 = (-1/((water_flowmap_strength_add.y-water_flowmap_strength_add.x)*water_flowmap_range_1),
      water_flowmap_strength_add.y/(water_flowmap_strength_add.y-water_flowmap_strength_add.x), water_flowmap_strength_add.z, water_flowmap_strength_add.w);
    water_flowmap_foam_displacement@f1 = (water_flowmap_foam_displacement);
    water_flowmap_foam_softness@f1 = (water_flowmap_foam_softness);
    water_flowmap_multiplier@f1 = (water_flowmap_multiplier);
    water_flowmap_blend@f1 = (water_flowmap_blend);
    water_fluid_strength@f4 = water_fluid_strength;
    water_fluid_brightness@f3 = water_fluid_brightness;
    water_flowmap_bump_scale@f3 = (1.0 / max(world_to_flowmap_add_2a.x, 0.0001), 1.0 / max(water_flowmap_bump_scale * water_fluid_strength.y * water_flowmap_foam_displacement, 0.0001), 1.0 / max(world_to_flowmap_add_2a.y, 0.0001));
    water_flowmap_cascades@i1 = (water_flowmap_cascades);
  }

  hlsl(code) {
    #define has_water_flowmap 1
    #define cascadesTexelScale (cascadesTexelScale_current_time_water_wind_strength.x)
    #define current_time (cascadesTexelScale_current_time_water_wind_strength.y)
    #define water_wind_strength (cascadesTexelScale_current_time_water_wind_strength.z)
    #define water_flowmap_fading (water_flowmap_fading_inv_fading.x)
    #define inv_water_flowmap_fading (water_flowmap_fading_inv_fading.y)
    #define water_flowmap_tex_add_0_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_add_1a_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_add_1b_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_add_2a_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_add_2b_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_blur_1a_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_blur_1b_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_blur_2a_samplerstate water_flowmap_tex_samplerstate
    #define water_flowmap_tex_blur_2b_samplerstate water_flowmap_tex_samplerstate
  }

  hlsl(code) {
    void calcWaterFlowmapParams(float3 worldPos, float viewDist, out float3 worldPos_a, out float3 worldPos_b, out float viewDist_a, out float viewDist_b, out float crossFade)
    {
      float2 windVec = wind_dir_speed.xy * cascadesTexelScale * (water_wind_strength + 0.5);

      float2 flowmapSample = float2(0, 0);

      float2 flowmapUV = worldPos.xz * world_to_flowmap.xy + world_to_flowmap.zw;
      if (all(flowmapUV >= 0 && flowmapUV <= 1))
      {
        float2 flowmapVec = tex2Dlod(water_flowmap_tex, float4(flowmapUV, 0, 0)).xy - 0.5;
        flowmapSample += flowmapVec * water_flowmap_strength.x;
      }

      float2 flowmapUVAdd0 = worldPos.xz * world_to_flowmap_add_0.xy + world_to_flowmap_add_0.zw;
      if (all(flowmapUVAdd0 >= 0 && flowmapUVAdd0 <= 1))
      {
        float flowmapStrengthAdd0 = saturate(viewDist * water_flowmap_strength_add_0.x + water_flowmap_strength_add_0.y) * water_flowmap_strength_add_0.z;
        float2 flowmapSampleAdd0 = tex2Dlod(water_flowmap_tex_add_0, float4(flowmapUVAdd0, 0, 0)).xy;
        flowmapSample += flowmapSampleAdd0 * flowmapStrengthAdd0;
      }

      ##if (max_flowmap_cascades == three)
        BRANCH
        if (water_flowmap_cascades >= 3)
        {
          float4 flowmapUVAdd2 = worldPos.xzxz * world_to_flowmap_add_2a + world_to_flowmap_add_2b;
          if (all(flowmapUVAdd2 >= 0 && flowmapUVAdd2 <= 1))
          {
            float2 flowmapSampleAdd2a = tex2Dlod(water_flowmap_tex_add_2a, float4(flowmapUVAdd2.xy, 0, 0)).xy;
            float2 flowmapSampleAdd2b = tex2Dlod(water_flowmap_tex_add_2b, float4(flowmapUVAdd2.zw, 0, 0)).xy;
            flowmapSampleAdd2a *= saturate(2 - 2 * length(flowmapUVAdd2.xy * 2 - 1));
            flowmapSampleAdd2b *= saturate(2 - 2 * length(flowmapUVAdd2.zw * 2 - 1));
            float2 flowmapSampleAdd2 = lerp(flowmapSampleAdd2a, flowmapSampleAdd2b, water_flowmap_blend);
            if (length(flowmapSampleAdd2) > 1)
              flowmapSampleAdd2 = normalize(flowmapSampleAdd2);
            flowmapSample -= flowmapSampleAdd2 * water_fluid_strength.w;
          }
        }
      ##endif

      float2 flowmapVec = (flowmapSample - windVec) * water_flowmap_fading;
      float flowmapTime = current_time * inv_water_flowmap_fading;
      float2 flowmapVec_a = flowmapVec * frac(flowmapTime + 0.0);
      float2 flowmapVec_b = flowmapVec * frac(flowmapTime + 0.5);

      worldPos_a = worldPos;
      worldPos_b = worldPos;

      worldPos_a.xz += flowmapVec_a * water_flowmap_multiplier;
      worldPos_b.xz += flowmapVec_b * water_flowmap_multiplier;

      viewDist_a = length(world_view_pos - worldPos_a);
      viewDist_b = length(world_view_pos - worldPos_b);

      crossFade = abs(frac(flowmapTime) * 2 - 1);
      crossFade = smoothstep(0, 1, crossFade);
    }

    float getWaterFlowmapDisplacement(float3 worldPos)
    {
      BRANCH
      if (water_flowmap_cascades >= 2)
      {
        float flowmapSample = 0;
        float4 flowmapUVAdd1 = worldPos.xzxz * world_to_flowmap_add_1a + world_to_flowmap_add_1b;
        if (all(flowmapUVAdd1 >= 0 && flowmapUVAdd1 <= 1))
        {
          float flowmapStrengthAdd1 = water_flowmap_strength_add_1.w;
          float flowmapSampleAdd1a = tex2Dlod(water_flowmap_tex_blur_1a, float4(flowmapUVAdd1.xy, 0, 0)).x;
          float flowmapSampleAdd1b = tex2Dlod(water_flowmap_tex_blur_1b, float4(flowmapUVAdd1.zw, 0, 0)).x;
          float flowmapSampleAdd1 = lerp(flowmapSampleAdd1a, flowmapSampleAdd1b, water_flowmap_blend);
          flowmapSample += flowmapSampleAdd1 * flowmapStrengthAdd1;
        }

        ##if (max_flowmap_cascades == three)
          BRANCH
          if (water_flowmap_cascades >= 3)
          {
            float4 flowmapUVAdd2 = worldPos.xzxz * world_to_flowmap_add_2a + world_to_flowmap_add_2b;
            if (all(flowmapUVAdd2 >= 0 && flowmapUVAdd2 <= 1))
            {
              float flowmapSampleAdd2a = tex2Dlod(water_flowmap_tex_blur_2a, float4(flowmapUVAdd2.xy, 0, 0)).x;
              float flowmapSampleAdd2b = tex2Dlod(water_flowmap_tex_blur_2b, float4(flowmapUVAdd2.zw, 0, 0)).x;
              flowmapSampleAdd2a *= saturate(2 - 2 * length(flowmapUVAdd2.xy * 2 - 1));
              flowmapSampleAdd2b *= saturate(2 - 2 * length(flowmapUVAdd2.zw * 2 - 1));
              float flowmapSampleAdd2 = lerp(flowmapSampleAdd2a, flowmapSampleAdd2b, water_flowmap_blend);
              flowmapSample *= flowmapSampleAdd2;
              flowmapSample += saturate(flowmapSampleAdd2 - water_fluid_strength.x) * water_fluid_strength.y;
            }
          }
        ##endif

        return flowmapSample * water_flowmap_foam_displacement;
      }
      return 0;
    }

    float3 getWaterFlowmapNormal(float3 worldPos)
    {
      ##if (max_flowmap_cascades == three)
        BRANCH
        if (water_flowmap_cascades >= 3)
        {
          float4 flowmapUVAdd2 = worldPos.xzxz * world_to_flowmap_add_2a + world_to_flowmap_add_2b;
          if (all(flowmapUVAdd2 >= 0 && flowmapUVAdd2 <= 1))
          {
            float flowmapSampleAdd2a = tex2Dlod(water_flowmap_tex_blur_2a, float4(flowmapUVAdd2.xy, 0, 0)).x;
            float flowmapSampleAdd2b = tex2Dlod(water_flowmap_tex_blur_2b, float4(flowmapUVAdd2.zw, 0, 0)).x;

            float2 flowmapNormalAdd2a;
            float2 flowmapNormalAdd2b;

            flowmapNormalAdd2a.x = flowmapSampleAdd2a - water_flowmap_tex_blur_2a.SampleLevel(water_flowmap_tex_blur_2a_samplerstate, flowmapUVAdd2.xy, 0, int2(1, 0)).x;
            flowmapNormalAdd2b.x = flowmapSampleAdd2b - water_flowmap_tex_blur_2b.SampleLevel(water_flowmap_tex_blur_2b_samplerstate, flowmapUVAdd2.zw, 0, int2(1, 0)).x;

            flowmapNormalAdd2a.y = flowmapSampleAdd2a - water_flowmap_tex_blur_2a.SampleLevel(water_flowmap_tex_blur_2a_samplerstate, flowmapUVAdd2.xy, 0, int2(0, 1)).x;
            flowmapNormalAdd2b.y = flowmapSampleAdd2b - water_flowmap_tex_blur_2b.SampleLevel(water_flowmap_tex_blur_2b_samplerstate, flowmapUVAdd2.zw, 0, int2(0, 1)).x;

            flowmapNormalAdd2a *= saturate(2 - 3 * length(flowmapUVAdd2.xy * 2 - 1));
            flowmapNormalAdd2b *= saturate(2 - 3 * length(flowmapUVAdd2.zw * 2 - 1));

            float2 flowmapNormalAdd2 = lerp(flowmapNormalAdd2a, flowmapNormalAdd2b, water_flowmap_blend);

            float3 flowmapNormal = normalize(float3(flowmapNormalAdd2.x, 1, flowmapNormalAdd2.y) * water_flowmap_bump_scale);
            return flowmapNormal;
          }
        }
      ##endif
      return float3(0, 1, 0);
    }
  }
endmacro

macro USE_WATER_FLOWMAP_FOAM()
  hlsl(ps) {
    float getWaterFlowmapFoam(float3 worldPos, float viewDist)
    {
      BRANCH
      if (water_flowmap_cascades >= 2)
      {
        float flowmapSample = 0;
        float4 flowmapUVAdd1 = worldPos.xzxz * world_to_flowmap_add_1a + world_to_flowmap_add_1b;
        if (all(flowmapUVAdd1 >= 0 && flowmapUVAdd1 <= 1))
        {
          float flowmapStrengthAdd1 = water_flowmap_strength_add_1.w;
          float flowmapSampleAdd1a = tex2Dlod(water_flowmap_tex_add_1a, float4(flowmapUVAdd1.xy, 0, 0)).z;
          float flowmapSampleAdd1b = tex2Dlod(water_flowmap_tex_add_1b, float4(flowmapUVAdd1.zw, 0, 0)).z;
          float flowmapSampleAdd1 = lerp(flowmapSampleAdd1a, flowmapSampleAdd1b, water_flowmap_blend);
          flowmapSample += flowmapSampleAdd1 * flowmapStrengthAdd1;
        }
        ##if (max_flowmap_cascades == three)
        BRANCH
        if (water_flowmap_cascades >= 3)
        {
          float4 flowmapUVAdd2 = worldPos.xzxz * world_to_flowmap_add_2a + world_to_flowmap_add_2b;
          if (all(flowmapUVAdd2 >= 0 && flowmapUVAdd2 <= 1))
          {
            float flowmapSampleAdd2a = tex2Dlod(water_flowmap_tex_add_2a, float4(flowmapUVAdd2.xy, 0, 0)).z;
            float flowmapSampleAdd2b = tex2Dlod(water_flowmap_tex_add_2b, float4(flowmapUVAdd2.zw, 0, 0)).z;
            flowmapSampleAdd2a *= saturate(2 - 2 * length(flowmapUVAdd2.xy * 2 - 1));
            flowmapSampleAdd2b *= saturate(2 - 2 * length(flowmapUVAdd2.zw * 2 - 1));
            float flowmapSampleAdd2 = lerp(flowmapSampleAdd2a, flowmapSampleAdd2b, water_flowmap_blend);
            flowmapSampleAdd2 *= saturate(1 - length(flowmapUVAdd2 * 2 - 1) * 0.7);
            flowmapSample *= flowmapSampleAdd2 * water_fluid_brightness.x;
            flowmapSample += pow(flowmapSampleAdd2 * water_fluid_brightness.y, water_fluid_brightness.z);
          }
        }
        ##endif
        return flowmapSample * water_flowmap_multiplier;
      }

      return 0;
    }
  }
endmacro

macro FX_USE_WATER_FLOWMAP(stage)
  if (water_flowmap_tex != NULL)
  {
    (stage)
    {
      water_flowmap_tex@smp2d = water_flowmap_tex;
      world_to_flowmap@f4 = world_to_flowmap;
      water_flowmap_fx_speed@f1 = (-water_flowmap_fx_speed * water_flowmap_strength.x, 0, 0, 0);
    }
    hlsl(stage)
    {
      #define DAFX_USE_WATER_FLOWMAP 1
      float3 getWaterFlowmapVec(float3 worldPos)
      {
        float2 flowmapUV = worldPos.xz * world_to_flowmap.xy + world_to_flowmap.zw;
        float2 flowmapVec = tex2Dlod(water_flowmap_tex, float4(flowmapUV, 0, 0)).xy - 0.5;
        flowmapVec *= water_flowmap_fx_speed;
        return float3(flowmapVec.x, 0, flowmapVec.y);
      }
    }
  }
endmacro

macro FX_USE_WATER_FLOWMAP_ADD(stage)
  if (water_flowmap_tex_add_0 != NULL)
  {
    (stage)
    {
      water_flowmap_tex_add_0@smp2d = water_flowmap_tex_add_0;
      world_to_flowmap_add_0@f4 = world_to_flowmap_add_0;
      water_flowmap_fx_speed@f1 = (-water_flowmap_fx_speed * water_flowmap_strength_add.z, 0, 0, 0);
    }
    hlsl(stage)
    {
      #define DAFX_USE_WATER_FLOWMAP 1
      float3 getWaterFlowmapVec(float3 worldPos)
      {
        float2 flowmapUVAdd0 = worldPos.xz * world_to_flowmap_add_0.xy + world_to_flowmap_add_0.zw;
        float2 flowmapSampleAdd0 = tex2Dlod(water_flowmap_tex_add_0, float4(flowmapUVAdd0, 0, 0)).xy;
        float2 flowmapVec = flowmapSampleAdd0 * water_flowmap_fx_speed;
        return float3(flowmapVec.x, 0, flowmapVec.y);
      }
    }
  }
endmacro

macro USE_WATER_FLOWMAP_ADD(stage)
  if (water_flowmap_tex_add_0 != NULL)
  {
    (stage)
    {
      water_flowmap_tex_add_0@smp2d = water_flowmap_tex_add_0;
      world_to_flowmap_add_0@f4 = world_to_flowmap_add_0;
      water_flowmap_strength_add@f1 = water_flowmap_strength_add.z;
    }
    hlsl(stage)
    {
      #define DAFX_USE_WATER_FLOWMAP 1
      float3 getWaterFlowmapVec(float3 worldPos)
      {
        float2 flowmapUVAdd0 = worldPos.xz * world_to_flowmap_add_0.xy + world_to_flowmap_add_0.zw;
        float2 flowmapSampleAdd0 = tex2Dlod(water_flowmap_tex_add_0, float4(flowmapUVAdd0, 0, 0)).xy;
        float2 flowmapVec = flowmapSampleAdd0 * water_flowmap_strength_add;
        return float3(flowmapVec.x, 0, flowmapVec.y);
      }
    }
  }
endmacro
